import org.apache.avro.tool.SpecificCompilerTool

buildscript {
    dependencies {
        classpath "org.apache.avro:avro-tools:1.12.0"
    }
}

apply from: "${project.rootDir}/gradle/in-test-generated.gradle"

dependencies {
    api project(":annotation-processor-common")
    api project(":kora-app-annotation-processor")

    testImplementation project(":avro:avro-common")
    testImplementation testFixtures(project(":annotation-processor-common"))
}

tasks.register("generateAvroClasses") {
    group("build")

    var inputDir = "$projectDir/src/test/resources/avro"
    var outputDir = "$buildDir/generated/sources/avro"
    inputs.dir(inputDir)
    outputs.dir(outputDir)
    logging.captureStandardOutput(LogLevel.INFO);
    logging.captureStandardError(LogLevel.ERROR)

    doFirst {
        delete outputDir
    }

    doLast {
        var params = ["-bigDecimal", "schema", inputDir.toString(), outputDir.toString()]
        new SpecificCompilerTool().run(System.in, System.out, System.err, params)
    }
}

test.dependsOn(tasks.generateAvroClasses)
