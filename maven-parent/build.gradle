plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    constraints {
        def addModule = { Project p ->
            api(p)
        }

        var added = new HashSet<String>()
        project.rootProject.subprojects { Project p ->
            if (p.parent.name == 'internal') return
            if (!p.subprojects.isEmpty()) return
            if (added.add(p.name)) {
                addModule(p)
            }
        }
    }
}


def modifyPom(Node pom) {
    pom.appendNode('properties').appendNode('java.version', '17')
    pom.appendNode('test').replaceNode {
        delegate.build {
            plugins {
                plugin {
                    groupId "org.apache.maven.plugins"
                    artifactId "maven-compiler-plugin"
                    delegate.version "3.8.1"
                    configuration {
                        release '${java.version}'
                        source '${java.version}'
                        target '${java.version}'
                        compilerArgs {
                            arg "-parameters"
                        }
                        annotationProcessorPaths {
                            path {
                                groupId "ru.tinkoff.kora"
                                artifactId "annotation-processors"
                                delegate.version project.version
                            }
                        }
                    }
                }
                plugin {
                    groupId "org.apache.maven.plugins"
                    artifactId "maven-surefire-plugin"
                    delegate.version "2.22.2"
                    configuration {
                        argLine "--enable-preview"
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'kora-parent'
            from components.javaPlatform
            // remove scope information from published BOM
            pom {
                withXml {
                    modifyPom(asNode())
                }
                name = "Kora parent pom"
                description = "Kora Bill-Of-Materials (BOM)"
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://github.com/kora-projects/kora/blob/master/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/kora-projects/kora'
                    connection = 'scm:git:git@github.com/kora-projects/kora.git'
                    developerConnection = 'scm:git:git@github.com/kora-projects/kora.git'
                }
                url = 'https://github.com/kora-projects/kora'
                developers {
                    developer {
                        id = 'a.otts'
                        name = 'Aleksei Otts'
                        email = 'eld0727@mail.ru'
                    }
                    developer {
                        id = 'a.duyun'
                        name = 'Anton Duyun'
                        email = 'anton.duyun@gmail.com'
                    }
                    developer {
                        id = 'a.kurako'
                        name = 'Anton Kurako'
                        email = 'goodforgod.dev@gmail.com'
                    }
                    developer {
                        id = 'a.yakovlev'
                        name = 'Artem Yakovlev'
                        email = 'jakart89@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'build'
            url = project.rootProject.layout.buildDirectory.dir("publishing-repository")
        }
        maven {
            name = 'snapshot'
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            credentials {
                username = System.getenv().getOrDefault("REPO_USER", "")
                password = System.getenv().getOrDefault("REPO_PASSWORD", "")
            }
        }
    }
}

signing {
    required = false
    sign publishing.publications.maven
}
