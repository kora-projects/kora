plugins {
    id 'com.google.protobuf' version "0.9.3"
}

dependencies {
    api project(':annotation-processor-common')

    implementation project(':kora-app-annotation-processor')

    testImplementation libs.grpc.java.gen
    testImplementation libs.grpc.protobuf
    testImplementation project(':grpc:grpc-client')
    testImplementation project(':config:config-annotation-processor')
    testImplementation testFixtures(project(':annotation-processor-common'))
    testImplementation 'javax.annotation:javax.annotation-api:1.3.2'
}

apply from: "${project.rootDir}/gradle/in-test-generated.gradle"

def grpcVersion = libs.versions.grpc.java.get()

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.9"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        ofSourceSet("test").forEach {
            it.plugins {
                grpc
            }
        }
    }
}

sourceSets {
    test {
        java {
            srcDir project.file('build/generated/source/proto/test/grpc')
            srcDir project.file('build/generated/source/proto/test/java')
        }
    }
}
